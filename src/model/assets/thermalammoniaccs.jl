struct ThermalAmmoniaCCS{T} <: AbstractAsset
    id::AssetId
    thermalammoniaccs_transform::Transformation
    nh3_edge::Union{Edge{<:Ammonia},EdgeWithUC{<:Ammonia}}
    elec_edge::Edge{<:Electricity}
    fuel_edge::Edge{<:T}
    co2_edge::Edge{<:CO2}
    co2_captured_edge::Edge{<:CO2Captured}
end

ThermalAmmoniaCCS(id::AssetId, thermalammoniaccs_transform::Transformation, nh3_edge::Union{Edge{<:Ammonia},EdgeWithUC{<:Ammonia}}, elec_edge::Edge{<:Electricity},
fuel_edge::Edge{T}, co2_edge::Edge{<:CO2}, co2_captured_edge::Edge{<:CO2Captured}) where T<:Commodity =
    ThermalAmmoniaCCS{T}(id, thermalammoniaccs_transform, nh3_edge, elec_edge, fuel_edge, co2_edge, co2_captured_edge)

function default_data(t::Type{ThermalAmmoniaCCS}, id=missing, style="full")
    if style == "full"
        return full_default_data(t, id)
    else
        return simple_default_data(t, id)
    end
end

function full_default_data(::Type{ThermalAmmoniaCCS}, id=missing)
    return OrderedDict{Symbol,Any}(
        :id => id,
        :transforms => @transform_data(
            :timedata => "Ammonia",
            :emission_rate => 0.0091,            # 5% CO2 emission rate, 0.181048235160161 tons CO2 per MWh of CH4
            :capture_rate => 0.17195,             # 95% CO2 capture rate
            :electricity_consumption => 0.07342, # data from the literature https://www.sciencedirect.com/science/article/pii/S0306261920313453, 1.93 times the electricity consumption of a thermalammonia without CCS
            :fuel_consumption => 1.3095,        # MWh of CH4
            :investment_cost => 2720959.03, # 130% of the investment cost of a thermalammonia without CCS
            :fixed_om_cost => 109232.584,
            :variable_om_cost => 1.17195,
            :lifetime => 30,
            :constraints => Dict{Symbol, Bool}(
                :BalanceConstraint => true,
            ),
        ),
        :edges => Dict{Symbol,Any}(
            :elec_edge => @edge_data(
                :commodity => "Electricity",
            ),
            :nh3_edge => @edge_data(
                :commodity => "Ammonia",
                :has_capacity => true,
                :can_retire => true,
                :can_expand => true,
                :constraints => Dict{Symbol, Bool}(
                    :CapacityConstraint => true,
                    :RampingLimitConstraint => true,
                ),
            ),
            :fuel_edge => @edge_data(
                :commodity => missing,
            ),
            :co2_edge => @edge_data(
                :commodity=>"CO2",
                :co2_sink => missing,
            ),
            :co2_captured_edge=>@edge_data(
                :commodity=>"CO2Captured",
            ),
        ),
    )
end

function simple_default_data(::Type{ThermalAmmoniaCCS}, id=missing)
    return OrderedDict{Symbol,Any}(
        :id => id,
        :location => missing,
        :can_expand => true,
        :can_retire => true,
        :existing_capacity => 0.0,
        :capacity_size => 1.0,
        :timedata => "NaturalGas",
        :fuel_commodity => "NaturalGas",
        :co2_sink => missing,
        :uc => false,
        :investment_cost => 2720959.03,
        :fixed_om_cost => 109232.584,
        :variable_om_cost => 1.17195,
        :fuel_consumption => 1.3095,
        :electricity_consumption => 0.07342,
        :emission_rate => 0.0091,
        :capture_rate => 0.17195,
    )
end

function set_commodity!(::Type{ThermalAmmoniaCCS}, commodity::Type{<:Commodity}, data::AbstractDict{Symbol,Any})
    edge_keys = [:fuel_edge]
    if haskey(data, :fuel_commodity)
        data[:fuel_commodity] = string(commodity)
    end
    if haskey(data, :edges)
        for edge_key in edge_keys
            if haskey(data[:edges], edge_key)
                if haskey(data[:edges][edge_key], :commodity)
                    data[:edges][edge_key][:commodity] = string(commodity)
                end
            end
        end
    end
end

function make(asset_type::Type{ThermalAmmoniaCCS}, data::AbstractDict{Symbol,Any}, system::System)
    id = AssetId(data[:id])

    @setup_data(asset_type, data, id)

    thermalammoniaccs_key = :transforms
    @process_data(
        transform_data, 
        data[thermalammoniaccs_key], 
        [
            (data[thermalammoniaccs_key], key),
            (data[thermalammoniaccs_key], Symbol("transform_", key)),
            (data, Symbol("transform_", key)),
            (data, key),
        ]
    )
    thermalammoniaccs_transform = Transformation(;
        id = Symbol(id, "_", thermalammoniaccs_key),
        timedata = system.time_data[Symbol(transform_data[:timedata])],
        constraints = get(transform_data, :constraints, [BalanceConstraint()]),
    )

    elec_edge_key = :elec_edge
    @process_data(
        elec_edge_data, 
        data[:edges][elec_edge_key], 
        [
            (data[:edges][elec_edge_key], key),
            (data[:edges][elec_edge_key], Symbol("elec_", key)),
            (data, Symbol("elec_", key)),
        ]
    )
    @start_vertex(
        elec_start_node,
        elec_edge_data,
        Electricity,
        [(elec_edge_data, :start_vertex), (data, :location)]
    )
    elec_end_node = thermalammoniaccs_transform
    elec_edge = Edge(
        Symbol(id, "_", elec_edge_key),
        elec_edge_data,
        system.time_data[:Electricity],
        Electricity,
        elec_start_node,
        elec_end_node,
    )

    nh3_edge_key = :nh3_edge
    @process_data(
        nh3_edge_data, 
        data[:edges][nh3_edge_key], 
        [
            (data[:edges][nh3_edge_key], key),
            (data[:edges][nh3_edge_key], Symbol("nh3_", key)),
            (data, Symbol("nh3_", key)),
            (data, key), 
        ]
    )
    nh3_start_node = thermalammoniaccs_transform
    @end_vertex(
        nh3_end_node,
        nh3_edge_data,
        Ammonia,
        [(nh3_edge_data, :end_vertex), (data, :location)],
    )

    # Check if the edge has unit commitment constraints
    has_uc = get(nh3_edge_data, :uc, false)
    EdgeType = has_uc ? EdgeWithUC : Edge
    # Create the nh3 edge with the appropriate type
    nh3_edge = EdgeType(
        Symbol(id, "_", nh3_edge_key),
        nh3_edge_data,
        system.time_data[:Ammonia],
        Ammonia,
        nh3_start_node,
        nh3_end_node,
    )
    if has_uc
        uc_constraints = [MinUpTimeConstraint(), MinDownTimeConstraint()]
        for c in uc_constraints
            if !(c in nh3_edge.constraints)
                push!(nh3_edge.constraints, c)
            end
        end
        nh3_edge.startup_fuel_balance_id = :energy
    end

    fuel_edge_key = :fuel_edge
    @process_data(
        fuel_edge_data, 
        data[:edges][fuel_edge_key], 
        [
            (data[:edges][fuel_edge_key], key),
            (data[:edges][fuel_edge_key], Symbol("fuel_", key)),
            (data, Symbol("fuel_", key)),
        ]
    )
    commodity_symbol = Symbol(fuel_edge_data[:commodity])
    commodity = commodity_types()[commodity_symbol]
    @start_vertex(
        fuel_start_node,
        fuel_edge_data,
        commodity,
        [(fuel_edge_data, :start_vertex), (data, :location)],
    )
    fuel_end_node = thermalammoniaccs_transform
    fuel_edge = Edge(
        Symbol(id, "_", fuel_edge_key),
        fuel_edge_data,
        system.time_data[commodity_symbol],
        commodity,
        fuel_start_node,
        fuel_end_node,
    )

    co2_edge_key = :co2_edge
    @process_data(
        co2_edge_data, 
        data[:edges][co2_edge_key], 
        [
            (data[:edges][co2_edge_key], key),
            (data[:edges][co2_edge_key], Symbol("co2_", key)),
            (data, Symbol("co2_", key)),
        ]
    )
    co2_start_node = thermalammoniaccs_transform
    @end_vertex(
        co2_end_node,
        co2_edge_data,
        CO2,
        [(co2_edge_data, :end_vertex), (data, :co2_sink), (data, :location)],
    )
    co2_edge = Edge(
        Symbol(id, "_", co2_edge_key),
        co2_edge_data,
        system.time_data[:CO2],
        CO2,
        co2_start_node,
        co2_end_node,
    )

    co2_captured_edge_key = :co2_captured_edge
    @process_data(
        co2_captured_edge_data, 
        data[:edges][co2_captured_edge_key], 
        [
            (data[:edges][co2_captured_edge_key], key),
            (data[:edges][co2_captured_edge_key], Symbol("co2_captured_", key)),
            (data, Symbol("co2_captured_", key)),
        ]
    )
    co2_captured_start_node = thermalammoniaccs_transform
    @end_vertex(
        co2_captured_end_node,
        co2_captured_edge_data,
        CO2Captured,
        [(co2_captured_edge_data, :end_vertex), (data, :location)],
    )
    co2_captured_edge = Edge(
        Symbol(id, "_", co2_captured_edge_key),
        co2_captured_edge_data,
        system.time_data[:CO2Captured],
        CO2Captured,
        co2_captured_start_node,
        co2_captured_end_node,
    )

    thermalammoniaccs_transform.balance_data = Dict(
        :energy => Dict(
            nh3_edge.id => get(transform_data, :fuel_consumption, 1.3095),
            fuel_edge.id => 1.0,
        ),
        :electricity => Dict(
            nh3_edge.id => get(transform_data, :electricity_consumption, 0.07342),
            elec_edge.id => 1.0
        ),
        :emissions => Dict(
            fuel_edge.id => get(transform_data, :emission_rate, 0.0091),
            co2_edge.id => 1.0,
        ),
        :capture => Dict(
            fuel_edge.id => get(transform_data, :capture_rate, 0.17195),
            co2_captured_edge.id => 1.0,
        ),
    )
 

    return ThermalAmmoniaCCS(id, thermalammoniaccs_transform, nh3_edge, elec_edge, fuel_edge, co2_edge, co2_captured_edge)
end 